apply plugin: "java"
apply plugin: "moe"
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceSets.main.java.srcDirs = ["src/"]
sourceSets.test.java.srcDirs = ["tests/"]

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

dependencies {
    implementation project(":gdx-fireapp-core")
    implementation libs.libgdx.iosmoe
    testImplementation libs.junit
    testImplementation libs.powermock.junit
    testImplementation libs.powermock.junitRule
    testImplementation libs.powermock.api
    testImplementation libs.powermock.xstream
}

eclipse.project {
    name = "gdx-fireapp-io-smoe"
}

ext {
    appModuleName = 'GDX Fireapp iOS-moe'
}

apply from: "" + project.rootDir + "/gradle/javadoc.gradle"
apply from: "" + project.rootDir + "/gradle/pom.gradle"

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class"
    exclude "**/GdxIOSAppTest.class"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            version = rootProject.version
            artifactId = 'gdx-fireapp-ios-moe'
            artifact jar
            artifact sourceJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', libraryDescription)
                root.appendNode('name', libraryName)
                root.appendNode('url', siteUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

apply from: "" + project.rootDir + "/gradle/bintray.gradle"